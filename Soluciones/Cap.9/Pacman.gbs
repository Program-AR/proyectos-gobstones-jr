/*
   PROYECTO: Pacman
   AUTOR: Pablo E. -Fidel- Martínez López
   E-MAIL: fidel@unq.edu.ar
   ENLACE: 
   FECHA: marzo 2017
   VERSIÓN: 1.0
   LICENCIA: GPL.v2.0
*/

interactive program {
  K_ARROW_UP    -> { HacerJugadaDelPacman(Norte)       }
  K_ARROW_RIGHT -> { HacerJugadaDelPacman(Este)        }
  K_ARROW_DOWN  -> { HacerJugadaDelPacman(Sur)         }
  K_ARROW_LEFT  -> { HacerJugadaDelPacman(Oeste)       }
  TIMEOUT(1000) -> { HacerJugadaDelFantasma() }
}

function pacman()   { return(Negro) }
function fantasma() { return(Rojo)  }
function pastilla() { return(Verde) }
function pared()    { return(Azul)  }

procedure HacerJugadaDelPacman(direcciónDelPacman) {
  MoverPersonaje(pacman(), direcciónDelPacman)
  if (hayPersonaje(fantasma())) { BOOM("Game over: te comió el fantasma") }
  if (hayPersonaje(pastilla())) {
      Sacar(pastilla()) 
      VerificarSiGanóElJuego()
    }
  HacerJugadaDelFantasma()
}

procedure VerificarSiGanóElJuego() {
  if (not hayMásPastillas()) { BOOM("¡Ganaste!") }
}

procedure MoverPersonaje(personajeAMover, direcciónAMover) {
  SacarPersonaje(personajeAMover)
  MoverSiPuede_(direcciónAMover)
  if (hayPersonaje(pared())) { Mover(opuesto_(direcciónAMover)) }
  PonerPersonaje(personajeAMover, direcciónAMover)
}

procedure SacarPersonaje(personajeASacar) { SacarTodas_(personajeASacar) }

procedure PonerPersonaje(personajeAPoner, direcciónAMirar) {
  if (direcciónAMirar == Norte) { Poner__Veces(personajeAPoner, 1) }
  if (direcciónAMirar == Este)  { Poner__Veces(personajeAPoner, 2) }
  if (direcciónAMirar == Sur)   { Poner__Veces(personajeAPoner, 3) }
  if (direcciónAMirar == Oeste) { Poner__Veces(personajeAPoner, 4) }
}

procedure HacerJugadaDelFantasma() {
  BuscarPersonaje_(fantasma())
  estáAlSE := hayPersonajeEnCuadrante(pacman(), Este,  Sur) 
  estáAlSO := hayPersonajeEnCuadrante(pacman(), Oeste, Sur)
  estáAlNE := hayPersonajeEnCuadrante(pacman(), Este,  Norte)
  estáAlNO := hayPersonajeEnCuadrante(pacman(), Oeste, Norte)
  
  if        (estáAlSE && estáAlSO) 
            { MoverFantasma(Sur, Este) } 
  else { if (estáAlNE && estáAlNO) 
            { MoverFantasma(Norte, Oeste) } 
  else { if (estáAlSE) 
            { MoverFantasma(Este, Sur) } 
  else { if (estáAlSO) 
            { MoverFantasma(Oeste, Sur) } 
  else { if (estáAlNO) 
            { MoverFantasma(Oeste, Norte) } 
  else      { MoverFantasma(Este, Norte) }
  }}}}
  BuscarPersonaje_(pacman())
}

procedure MoverFantasma(direcciónPrincipal, direcciónSecundaria) {
  if (puedeMover(direcciónPrincipal)) 
       { Mover(direcciónPrincipal)
         if (hayPersonaje(pared())) { direcciónDefinitiva := direcciónSecundaria }
         else                       { direcciónDefinitiva := direcciónPrincipal }
         Mover(opuesto_(direcciónPrincipal))
       } 
  else { direcciónDefinitiva := direcciónSecundaria }
  
  MoverPersonaje(fantasma(), direcciónDefinitiva)
  if (hayPersonaje(pacman())) { BOOM("Game Over: te comió el fantasma") }
}

function hayMásPastillas() {
    totalDePastillas := 0
    IrAlBorde(Sur) IrAlBorde(Oeste)
    ancho := distanciaAlBorde_(Este)
    while (hayCeldaSiguiente__(Este, Norte)) {
        if (hayPersonaje(pastilla())) 
          { totalDePastillas := totalDePastillas + 1 }
        IrACeldaSiguiente___(Este, Norte, ancho)
    }
    if (hayPersonaje(pastilla())) 
      { totalDePastillas := totalDePastillas + 1 }
    return (totalDePastillas > 0)
}

function hayPersonaje(personaje) { return(hayBolitas(personaje)) }

procedure BuscarPersonaje(personajeABuscar) {
  IrAlBorde(Oeste)   IrAlBorde(Norte)
  BuscarEnCuadrante(personajeABuscar, Este, Sur)
}

function hayPersonajeEnCuadrante(personaje, dirección1, dirección2) {
  BuscarEnCuadrante(personaje, dirección1, dirección2)
  return (hayPersonaje(personaje))
}

procedure BuscarEnCuadrante(personaje, direcciónHorizontal, direcciónVertical) {
  anchoDelCuadrante := distanciaAlBorde_(direcciónHorizontal) 
  while (not hayPersonaje(personaje) && hayCeldaSiguiente__(direcciónHorizontal,direcciónVertical))
     { IrACeldaSiguiente___(direcciónHorizontal,direcciónVertical,anchoDelCuadrante) }
}






