/*
   PROYECTO: Pacman
   AUTOR: Pablo E. -Fidel- Martínez López
   E-MAIL: fidel@unq.edu.ar
   ENLACE: 
   FECHA: marzo 2017
   VERSIÓN: 1.0
   LICENCIA: GPL.v2.0
*/

interactive program {
  K_ARROW_LEFT  -> { HacerJugadaDelPacman(Oeste) }
  K_ARROW_RIGHT -> { HacerJugadaDelPacman(Este)  }
  K_ARROW_UP    -> { HacerJugadaDelPacman(Norte) }
  K_ARROW_DOWN  -> { HacerJugadaDelPacman(Sur)   }
  _             -> { HacerJugadaDelFantasma()    }
}

function pacman()   { return(Negro) }
function pared()    { return(Azul)  }
function pastilla() { return(Verde) }
function fantasma() { return(Rojo)  }

function hay(personaje) { return(hayBolitas(personaje)) }
function hayEnCuadrante(personaje, d1, d2) {
  BuscarEnCuadrante(personaje, d1, d2)
  return (hay(personaje))
}

procedure HacerJugadaDelPacman(dir) {
  MoverPersonaje(pacman(), dir)
  if (hay(fantasma())) { BOOM("Game over") }
  if (hay(pastilla())) 
    { Sacar(pastilla()) 
      if (not hayMasPastillas()) { BOOM("Ganaste!!") }
    }
  HacerJugadaDelFantasma()
}

procedure MoverPersonaje(personaje, dir) {
  SacarPersonaje(personaje)
  MoverSiPuede(dir)
  if (hay(pared())) { Mover(opuesto(dir)) }
  PonerPersonajeMirandoAl(personaje, dir)
}

procedure SacarPersonaje(personaje) { SacarTodas(personaje) }

procedure PonerPersonajeMirandoAl(color, dir) {
  if (dir == Norte) { Poner__Veces(color, 1) }
  if (dir == Este)  { Poner__Veces(color, 2) }
  if (dir == Sur)   { Poner__Veces(color, 3) }
  if (dir == Oeste) { Poner__Veces(color, 4) }
}

procedure HacerJugadaDelFantasma() {
  BuscarPersonaje(fantasma())
  if        (hayEnCuadrante(pacman(), Este, Sur)) 
            { MoverFantasma(Este, Sur) } 
  else { if (hayEnCuadrante(pacman(), Oeste, Sur)) 
            { MoverFantasma(Oeste, Sur) } 
  else { if (hayEnCuadrante(pacman(), Oeste, Norte)) 
            { MoverFantasma(Oeste, Norte) } 
  else      { MoverFantasma(Este, Norte) }
  }}
  if (hay(pacman())) { BOOM("Perdiste!") }
  BuscarPersonaje(pacman())
}

procedure MoverFantasma(d1, d2) {
  if (puedeMover(d1)) 
       { Mover(d1)
         if (hay(pared())) { dirDefinitiva := d2 }
         else              { dirDefinitiva := d1 }
         Mover(opuesto(d1))
       } 
  else { dirDefinitiva := d2 }
  
  MoverPersonaje(fantasma(), dirDefinitiva)
  if (hay(pacman())) { BOOM("Game Over") }
}

function hayMasPastillas() {
    totalPastillas := 0
    IrAlBorde(Sur) IrAlBorde(Oeste)
    ancho := distanciaAlBorde(Este)
    while (hayCeldaSiguiente(Este, Norte)) {
        if (hay(pastilla())) 
          { totalPastillas := totalPastillas + 1 }
        IrACeldaSiguiente(Este, Norte, ancho)
    }
    if (hay(pastilla())) 
      { totalPastillas := totalPastillas + 1 }
    return (totalPastillas > 0)
}



